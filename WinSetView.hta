<!--
WinSetView (Globally Set Explorer Folder Views)
Les Ferch, lesferch@gmail.com, GitHub repository created 2021-03-26
File 1 of 2: WinSetView.hta (GUI tool to select desired views)
-->

<html>
<meta charset="UTF-8">
<head>
<hta:application
  applicationname=WinSetView
  maximizebutton=no
  scroll=no
  selection=no
  singleinstance=yes
>
<style>
  body  {font-size:10pt; font-family:Arial}
  table {font-size:10pt; font-family:Arial; border-collapse:collapse}
  td    {padding: 0 15 0 0}
  #op   {margin-bottom:5pt}
  #th   {margin-bottom:5pt}
  #cs   {margin-bottom:5pt; margin-top:5pt; width:100%; height:40; white-space:nowrap; overflow:auto}
  #hb   {width:2em}
  #wn   {width:2em}
  #wp   {width:2em}
</style>
<title>Globally Set Explorer Folder Views</title>
</head>

<script language=VBScript>
Option Explicit
Const Ver = "1.1"
Dim Mode, NoGroup, Generic, TPMode, TPNoGrp, ShowExt, Columns, Msg, Choice, oFolder, oFiles
Dim MyPath, CmdLine, Params, oShell, Visible, oFSO, i, Chk, ArrCB, j, AppData, rbShow, RBSet, CBSet
Dim ColShow, ColMore, Label, Delim, CSVData, ArrCSV, CSVFile, oFile, DefData, UserData, OldData
Dim oReg, Key, SubKey, ArrKeys, Value, WinWidth, WinHeight, ScaleWidth, Resize, Result, NameWid, PathWid

'Set up an array of checkbox object names
ArrCB = Split("se,ng,gn,ch,dm,sz,ti,fn,dc,fv,tt,fp,da,fe,tc,ff,fa,tk,tp,pi,fo,pc,xg",",")

RBSet = "True,False,False,False,False,False,False,False,False,True,False,False,False,False,False"
CBSet = "True,True,True,True,True,True,True,False,False,False,False,False,False,False,False,False,False,False,False,False,False,True,True"
ColShow = "DateModified Size ItemType"
ColMore = "FileExtension ItemTypeText ContentType PerceivedType Kind DateCreated DateAccessed FileAttributes FileVersion"
ColMore = ColMore & " ItemFolderNameDisplay ItemFolderPathDisplay ItemFolderPathDisplayNarrow ItemPathDisplay FileOwner"
NameWid = "34"
PathWid = "34"
DefData = Ver & "," & RBSet & "," & CBSet & "," & NameWid & "," & PathWid & "," & ColShow & "," & ColMore
UserData = DefData
Set oShell = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")
WinWidth = 580
WinHeight = 530

Sub Window_onLoad

  CenterWindow WinWidth,WinHeight

  'Resize window if not 96 dpi
  Resize = False
  Value = oShell.RegRead("HKCU\Control Panel\Desktop\WindowMetrics\AppliedDPI")
  If Value > 96 Then
    'Custom scaling is set
    Resize = True
    ScaleWidth = Value/96
  Else
    'See if standard (125 or 150 percent) scaling is set
    Set oReg = GetObject("winmgmts://./root/default:StdRegProv")
    Key = "Control Panel\Desktop\PerMonitorSettings"
    Const HKEY_CURRENT_USER = &H80000001
    Result = oReg.EnumKey(HKEY_CURRENT_USER, Key, ArrKeys)
    If Result=0 Then
      'Assume first monitor in list is the one we want
      For Each SubKey In ArrKeys
        Exit For
      Next
      Value = oShell.RegRead("HKCU\" & Key & "\" & SubKey & "\DPIValue")
      If Value=1 Then ScaleWidth = 1.25: Resize = True
      If Value=2 Then ScaleWidth = 1.50: Resize = True
    End If
  End If
  If Resize Then
    WinWidth = WinWidth * ScaleWidth
    CenterWindow WinWidth,WinHeight
    WinHeight = Document.GetElementByID("all").ScrollHeight * 1.15
    CenterWindow WinWidth,WinHeight
  End If

  'Set up paths
  AppData = oShell.ExpandEnvironmentStrings("%APPDATA%") & "\WinSetView"
  If Not oFSO.FolderExists(AppData) Then oFSO.CreateFolder(AppData)
  CSVFile = AppData & "\WinSetView.csv"

  'Read in previous user selections
  If oFSO.FileExists(CSVFile) Then
    Set oFile = oFSO.OpenTextFile(CSVFile,1)
    UserData = oFile.ReadAll
    oFile.Close
  End If

  'See if there are any reg files in the backup folder
  Set oFolder = oFSO.GetFolder(AppData)
  Set oFiles = oFolder.Files
  rbShow = False
  For Each oFile In oFiles
    If LCase(oFSO.GetExtensionName(oFile.Name)) = "reg" Then rbShow = True
  Next

  'Update interface
  HTAStateRestore UserData
  If (UserData=DefData Or OldData) Then yb.Disabled = True
  If Not rbShow Then rb.Disabled = True

End Sub

Sub CenterWindow(widthX, heightY)
  self.ResizeTo widthX, heightY 
  self.MoveTo (screen.Width - widthX)/2, (screen.Height - heightY)/2
End Sub

'Set interface to values from CSVData
Sub HTAStateRestore(CSVData)
  ArrCSV = Split(CSVData,",")
  If Not ArrCSV(0) = Ver Then OldData=True: Exit Sub
  j = 1
  For i = 0 to 7
    vm(i).Checked = ArrCSV(j)
    j = j + 1
  Next 
  For i = 0 to 6
    xm(i).Checked = ArrCSV(j)
    j = j + 1
  Next
  For Each Chk in ArrCB
    Eval(Chk).Checked = ArrCSV(j)
    j = j + 1
  Next
  NameWid = ArrCSV(j)
  PathWid = ArrCSV(j+1)
  ColShow = ArrCSV(j+2)
  ColMore = ArrCSV(j+3)
  ViewOps
  wn.Value = NameWid
  wp.Value = PathWid
  cs.Value = ColShow
End Sub

'Save interface values to file
Sub HTAStateSave
  CSVData = Ver & ","
  For i = 0 to 7
    CSVData = CSVData & vm(i).Checked & ","
  Next 
  For i = 0 to 6
    CSVData = CSVData & xm(i).Checked & ","
  Next
  For Each Chk in ArrCB
    CSVData = CSVData & Eval(Chk).Checked & ","
  Next
  CSVData = CSVData & wn.Value & "," & wp.Value & ","
  CSVData = CSVData & ColShow & ","
  CSVData = CSVData & ColMore
  Set oFile = oFSO.OpenTextFile(CSVFile,2,True)
  oFile.Write CSVData
  oFile.Close
End Sub

'Run WinSetView.ps1 with parameters based on user selections
Sub Submit
  HTAStateSave
  ColShow = Replace(ColShow," ",",")
  ColMore = Replace(ColMore," ",",")

  If vm(0).Checked Then Mode = 1
  If vm(1).Checked Then Mode = 4
  If vm(2).Checked Then Mode = 2
  If vm(3).Checked Then Mode = 5
  If vm(4).Checked Then Mode = 3
  If vm(5).Checked Then Mode = 6
  If vm(6).Checked Then Mode = 7
  If vm(7).Checked Then Mode = 0

  If pc.Checked Then 
    If xm(0).Checked Then TPMode = 1
    If xm(1).Checked Then TPMode = 4
    If xm(2).Checked Then TPMode = 2
    If xm(3).Checked Then TPMode = 5
    If xm(4).Checked Then TPMode = 3
    If xm(5).Checked Then TPMode = 6
    If xm(6).Checked Then TPMode = 7
  Else
    TPMode = 0
  End If
  
  If ng.Checked Then NoGroup=1 Else NoGroup=0
  If xg.Checked Then TPNoGrp=1 Else TPNoGrp=0
  If gn.Checked Then Generic=1 Else Generic=0
  If se.Checked Then ShowExt=1 Else ShowExt=0
  If ch.Checked Then Columns=1 Else Columns=0

  NameWid=wn.Value
  PathWid=wp.Value
  
  ColShow = "'" & ColShow & "'"
  ColMore = "'" & ColMore & "'"
  Params = Mode & " " & ShowExt & " " & NoGroup & " " & Generic & " " & TPMode & " " & TPNoGrp
  Params = Params & " " & Columns & " " & ColShow & " " & ColMore & " " & NameWid & " " & PathWid
  Visible = 0
  RunScript
End Sub

Sub RunScript
  MyPath = LCase(document.location.pathname)
  MyPath = Left(MyPath,InStrRev(MyPath,"\"))
  CmdLine = "Powershell.exe -ExecutionPolicy Bypass " & Chr(34) & MyPath & "WinSetView.ps1" & Chr(34) & " " & Params
  oShell.Run CmdLine,Visible,False
  Window.Close
End Sub

'Restore Explorer foder views from backup reg file
'WinSetView.ps1 creates a unique backup file on each run
Sub Restore
  Params = "9"
  Visible = 1
  RunScript
End Sub

'Hide or show options based on current selections
Sub ViewOps
  If vm(7).Checked Then
    op.style.visibility = "hidden"
    co.style.visibility = "hidden"  
    th.style.visibility = "hidden"  
    mc.style.visibility = "hidden"
    Exit Sub
  End If  
  op.style.visibility = "visible"
  th.style.visibility = "visible"  
  mc.style.visibility = "visible"  
  If ch.Checked Then
    co.style.visibility = "visible"
  Else
    co.style.visibility = "hidden"  
  End If
  If pc.Checked Then
    mc.style.visibility = "visible"
  Else
    mc.style.visibility = "hidden"  
  End If
End Sub

'ColShow contains column headings to be displayed in Details view
'ColMore contains additional right-click column heading options
Sub Mux(Label,Checked)
  ColShow = " " & Replace(ColShow,","," ") & " "
  ColMore = " " & Replace(ColMore,","," ") & " "
  If Checked Then
    ColShow = ColShow & " " & Label
    ColMore = Replace(ColMore," " & Label & " "," ")
  Else
    ColMore = ColMore & " " & Label
    ColShow = Replace(ColShow," " & Label & " "," ")
  End If
  ColShow = Trim(ColShow)
  ColMore = Trim(ColMore)
  ColShow = Replace(ColShow,"  "," ")
  ColMore = Replace(ColMore,"  "," ")
  cs.Value = ColShow
End Sub

Sub Help01
  Dim HH,HT
  HH = "Help Topic: Name and Path Column Widths"
  HT = HT & "The first number sets the width of the name column (first column) in Details view." & vbCRLF & vbCRLF
  HT = HT & "The second number Sets the width of any path columns enabled in Details view." & vbCRLF & vbCRLF
  HT = HT & "The value is specified in ems. 1 em ≈ 1 char" & vbCRLF & vbCRLF
  HT = HT & "Em size is relative to screen scaling. For example (at 96 dpi):" & vbCRLF & vbCRLF
  HT = HT & "1 em at 100% = 8 pixels" & vbCRLF
  HT = HT & "1 em at 125% = 10 pixels" & vbCRLF
  HT = HT & "1 em at 150% = 12 pixels" & vbCRLF & vbCRLF
  HT = HT & "Explorer uses ems internally for all its default column widths. This keeps the amount of text "
  HT = HT & "displayed in each column constant as screen scaling is changed."
  MsgBox HT,0+64,HH
End Sub
</script>

<body>
<div id=all>
  <input type=radio name=vm Checked onclick=ViewOps >Details
  <input type=radio name=vm onclick=ViewOps >List
  <input type=radio name=vm onclick=ViewOps >Tiles
  <input type=radio name=vm onclick=ViewOps >Content
  <input type=radio name=vm onclick=ViewOps >Small Icons
  <input type=radio name=vm onclick=ViewOps >Medium Icons
  <input type=radio name=vm onclick=ViewOps >Large Icons<br><br>
  <input type=radio name=vm onclick=ViewOps >Reset Views to Windows Defaults<br><br>
  <input type=checkbox id=se Checked >Show File Extensions
  <div id=op>
    <input type=checkbox id=ng Checked >No Grouping
    <input type=checkbox id=gn Checked >Make All Folders Generic<br><br>
    <input type=checkbox id=ch Checked onclick=ViewOps >Set Global Column Headings:<br>
  </div>
  <div id=co>
    <table>
    <tr><td>
    <input type=checkbox id=dm Checked onclick='Mux "DateModified",dm.Checked' >Date modified</td><td>
    <input type=checkbox id=sz Checked onclick='Mux "Size",sz.Checked' >Size</td><td>
    <input type=checkbox id=ti Checked onclick='Mux "ItemType",ti.Checked' >Item type</td><td>
    <input type=checkbox id=fn onclick='Mux "ItemFolderNameDisplay",fn.Checked' >Folder name</td><tr>
    <tr><td>
    <input type=checkbox id=dc onclick='Mux "DateCreated",dc.Checked' >Date created</td><td>
    <input type=checkbox id=fv onclick='Mux "FileVersion",fv.Checked' >File version</td><td>
    <input type=checkbox id=tt onclick='Mux "ItemTypeText",tt.Checked' >Type</td><td>
    <input type=checkbox id=fp onclick='Mux "ItemFolderPathDisplay",fp.Checked' >Folder path</td><tr>
    <tr><td>
    <input type=checkbox id=da onclick='Mux "DateAccessed",da.Checked' >Date accessed</td><td>
    <input type=checkbox id=fe onclick='Mux "FileExtension",fe.Checked' >File extension</td><td>
    <input type=checkbox id=tc onclick='Mux "ContentType",tc.Checked' >Content type</td><td>
    <input type=checkbox id=ff onclick='Mux "ItemFolderPathDisplayNarrow",ff.Checked' >Folder</td><tr>
    <tr><td>
    <input type=checkbox id=fa onclick='Mux "FileAttributes",fa.Checked' >Attributes</td><td>
    <input type=checkbox id=tk onclick='Mux "Kind",tk.Checked' >Kind</td><td>
    <input type=checkbox id=tp onclick='Mux "PerceivedType",tp.Checked' >Perceived type</td><td>
    <input type=checkbox id=pi onclick='Mux "ItemPathDisplay",pi.Checked' >Path</td><tr>
    <tr><td>
    <input type=checkbox id=fo onclick='Mux "FileOwner",fo.Checked' >Owner</td><tr>
    </table>
    <textarea id=cs readOnly=True></textarea><br>
    <input type=button id=hb value=? onclick=Help01>
    Name Column Width:<input type=text id=wn maxlength="2" value="34">
    Path Column Width:<input type=text id=wp maxlength="2" value="34"><br><br>
  </div>
  <div id=th>
    <input type=checkbox id=pc Checked onclick=ViewOps >Set views for "This PC" and "Network":<br>
  </div>
  <div id=mc>
    <input type=radio name=xm >Details
    <input type=radio name=xm Checked >List
    <input type=radio name=xm >Tiles
    <input type=radio name=xm >Content
    <input type=radio name=xm >Small Icons
    <input type=radio name=xm >Medium Icons
    <input type=radio name=xm >Large Icons<br>
    <input type=checkbox id=xg Checked >No Grouping<br><br>
  </div>
  <input type=button value=Submit onclick=Submit>
  <input type=button id=yb value="Last Run Settings" onclick='HTAStateRestore UserData'>
  <input type=button value="App Defaults" onclick='HTAStateRestore DefData'>
  <input type=button id=rb value="Restore from Backup" onclick=Restore>
</div>
</body>
</html>

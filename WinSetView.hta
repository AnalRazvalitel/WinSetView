<!--
WinSetView (Globally Set Explorer Folder Views)
Les Ferch, lesferch@gmail.com, GitHub repository created 2021-03-26
File 1 of 2: WinSetView.hta (GUI tool to select desired views)
-->
<html>
<meta charset="UTF-8">
<hta:application
  id=oHTA
  icon=powershell.exe
  applicationname=WinSetView
  maximizebutton=no
  scroll=no
  selection=no
  singleinstance=yes
>
<script language="VBScript">
Option Explicit
Window.ResizeTo 100,100
Window.MoveTo -2000,-2000
SetLocale(1033)
Const Ver = "1.5"
Dim Mode, NoGroup, Generic, TPMode, TPNoGrp, ShowExt, Columns, Msg, Choice, oFolder, oFiles, AllHTML
Dim CmdLine, Params, oShell, Visible, oFSO, i, Chk, ArrCB, j, AppData, rbShow, RBSet, CBSet
Dim ColShow, ColMore, Label, Delim, CSVData, ArrCSV, CSVFile, oFile, DefData, UserData, Centered
Dim WinWidth, WinHeight, ScaleWidth, Resize, Result, NameWid, PathWid, WinVer, HH2, HT2, TestFile
Dim LangData, LangDir, LangFile, ArrLang, Tag, Item, ArrHelp, HH1, HT1, FileName, ParamFile, X, Y
Dim Count, Pick, Language, oOption, LangCount, LangLast, TaggedHTML, LangIndex, SubMode, FirstRun

Set oShell = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")

'Get Windows version
WinVer = oShell.RegRead("HKLM\Software\Microsoft\Windows NT\CurrentVersion\ProductName")
WinVer = Trim(Replace(Mid(WinVer,9,2),"."," "))

'Check Windows version
If Not(WinVer="7" Or WinVer="8" Or WinVer="10") Then
  MsgBox "Windows 7, 8, or 10 is required.",vbCritical,"Error"
  Self.Close
End If

'Set up an array of checkbox object names
ArrCB = Split("se,ng,gn,ka,ch,fp,dm,sz,ti,ff,dc,fv,tt,pi,da,fe,tc,fn,fa,tk,tp,fo,pc,xg",",")

RBSet = "False,True,False,False,False,False,False,False,False,True,False,False,False,False,False"
CBSet = "True,True,True,False,True,True,True,True,True,False,False,False,False,False,False,False,False,False,False,False,False,False,True,True"
ColShow = "ItemFolderPathDisplay DateModified Size ItemType"
ColMore = "FileExtension ItemTypeText ContentType PerceivedType Kind DateCreated DateAccessed FileAttributes FileVersion"
ColMore = ColMore & " ItemFolderNameDisplay ItemFolderPathDisplay ItemFolderPathDisplayNarrow ItemPathDisplay FileOwner"
NameWid = "34"
PathWid = "34"
Language = "English"
DefData = Ver & "," & RBSet & "," & CBSet & "," & NameWid & "," & PathWid & "," & ColShow & "," & ColMore & "," & Language
UserData = DefData
LangDir = ".\Language\"
AppData = ".\AppData"

TestFile = ".\" & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".txt"
On Error Resume Next
Set oFile = oFSO.OpenTextFile(TestFile,2,True)
oFile.Write ""
oFile.Close
On Error Goto 0
If oFSO.FileExists(TestFile) Then 
  oFSO.DeleteFile(TestFile)
Else
  AppData = oShell.ExpandEnvironmentStrings("%APPDATA%") & "\WinSetView"
End If

LangData = "Globally Set Explorer Folder Views,Reset Views to Windows Defaults,"
LangData = LangData + "Details,List,Tiles,Content,Small Icons,Medium Icons,Large Icons,"
LangData = LangData + "Show File Extensions,No Grouping,Make All Folders Generic,Keep `Apply to Folders` Views,"
LangData = LangData + "Set Global Column Headings:,Date modified,Size,Item type,Folder name,"
LangData = LangData + "Date created,File version,Type,Folder path,Date accessed,File extension,"
LangData = LangData + "Content type,Folder,Attributes,Kind,Perceived type,Path,Owner,"
LangData = LangData + "Name Column Width:,Path Column Width:,Set views for `This PC` and `Network`:,"
LangData = LangData + "No Grouping,Submit,Last Run Settings,App Defaults,Restore from Backup"

HH1 = "Help Topic: Name and Path Column Widths"
HT1 = HT1 & "The first number sets the width of the name column (first column) in Details view." & vbCRLF & vbCRLF
HT1 = HT1 & "The second number Sets the width of any path columns enabled in Details view." & vbCRLF & vbCRLF
HT1 = HT1 & "The value is specified in ems. 1 em â‰ˆ 1 char" & vbCRLF & vbCRLF
HT1 = HT1 & "Em size is relative to screen scaling. For example (at 96 dpi):" & vbCRLF & vbCRLF
HT1 = HT1 & "1 em at 100% = 8 pixels" & vbCRLF
HT1 = HT1 & "1 em at 125% = 10 pixels" & vbCRLF
HT1 = HT1 & "1 em at 150% = 12 pixels" & vbCRLF & vbCRLF
HT1 = HT1 & "Explorer uses ems internally for all its default column widths. This keeps the amount of text "
HT1 = HT1 & "displayed in each column constant as screen scaling is changed."

HH2 = "Warning..."
HT2 = HT2 & "If `Make All Folders Generic` and `Keep `Apply to Folders` Views` are both checked, "
HT2 = HT2 & "only Downloads, Libraries, and Search Results will get their views from a view saved "
HT2 = HT2 & "with Explorer's `Apply to Folders` button."

'Set up paths
If Not oFSO.FolderExists(AppData) Then oFSO.CreateFolder(AppData)
CSVFile = AppData & "\WinSetView.csv"
ParamFile = AppData & "\WinSetViewParams.txt"

'Read in previous user selections
FirstRun = True
If oFSO.FileExists(CSVFile) Then
  FirstRun = False
  Set oFile = oFSO.OpenTextFile(CSVFile,1,,-1)
  UserData = oFile.ReadAll
  oFile.Close
  ArrCSV = Split(UserData,",")
  If Not ArrCSV(0) = Ver Then
    UserData = DefData
    FirstRun = True
  End If
End If

'See if there are any reg files in the backup folder
Set oFolder = oFSO.GetFolder(AppData)
Set oFiles = oFolder.Files
rbShow = False
For Each oFile In oFiles
  If LCase(oFSO.GetExtensionName(oFile.Name)) = "reg" Then rbShow = True
Next

Sub Window_onLoad
  Centered = False

  'Populate language dropdown menu
  LangCount = 0
  If oFSO.FolderExists(LangDir) Then
    Set oFolder = oFSO.GetFolder(LangDir)
    For Each oFile In oFolder.Files
      FileName = oFSO.GetBaseName(oFile)
      If Lcase(oFSO.getExtensionName(oFile.path)) = Lcase("txt") Then
        LangCount = LangCount + 1
        Set oOption = Document.CreateElement("Option")
        oOption.Text =  FileName 
        oOption.Value =  FileName 
        Lang.Add(oOption) 
      End If 
    Next
  End If
  
  If LangCount=0 Then
    Set oOption = Document.CreateElement("Option")
    oOption.Text =  "English" 
    oOption.Value =  "English" 
    Lang.Add(oOption) 
    lang.disabled = True 
  End If
  
  'Update interface selections
  HTAStateRestore UserData

  If FirstRun Then yb.Disabled = True
  If Not rbShow Then rb.Disabled = True

  If LangCount>0 Then
    LangIndex = 0
    LangLast = LangCount - 1
    For i = 0 To (LangLast)
      If LCase(Language)=LCase(lang.options(i).value) Then
        LangIndex = i
        Exit For
      End If
    Next
    lang.SelectedIndex = LangIndex
  End If

  TaggedHTML = Body.InnerHTML

  UpdateLang

  Set oOption = Nothing
  Set oFile = Nothing
  Set oFiles = Nothing
  Set oFolder = Nothing

End Sub

Sub CenterWindow(Width,Height)
  Self.ResizeTo Width,Height
  If Not Centered Then
    X = (screen.Width - Width) / 2
    Y = (screen.Height - Height) / 2
    Self.MoveTo X,Y
    Centered = True
  End If
End Sub

'Update labels with selected language
Sub UpdateLang

  'Read in language file
  If LangCount>0 Then
    LangIndex = lang.SelectedIndex
    Language = lang.options(LangIndex).value
    LangFile = LangDir & Language & ".txt"
    If oFSO.FileExists(LangFile) Then
      Set oFile = oFSO.OpenTextFile(LangFile,1,,-1)
      LangData = oFile.ReadAll
      oFile.Close
      Set oFile = Nothing
      ArrHelp = Split(LangData,"<>")
      HH1 = ArrHelp(1)
      HT1 = ArrHelp(2)
      HH2 = ArrHelp(3)
      HT2 = ArrHelp(4)
      LangData = Replace(LangData,VBCRLF,",")
    End If
  End If

  ArrLang = Split(LangData,",")

  AllHTML = TaggedHTML
  
  Document.title = ArrLang(0) & " " & Ver
  
  For i = 1 To 38
    Tag = "#" & i & "#"
    Item = ArrLang(i)
    Item = Replace(Item,"`",Chr(34))
    Item = Replace(Item,Tag,"")
    AllHTML = Replace(AllHTML,Tag,Item)
  Next
  AllHTML = Replace(AllHTML," ~","")
  Body.InnerHTML = AllHTML

  'ScaleDialog
  Self.ResizeTo 800,800 
  WinWidth = Document.GetElementByID("vb").ScrollWidth * 1.2
  Self.ResizeTo WinWidth,800 
  WinHeight = Document.GetElementByID("body").ScrollHeight * 1.08
  CenterWindow WinWidth,WinHeight

  lang.SelectedIndex = LangIndex
  
End Sub

'Set interface selections to values from CSVData
Sub HTAStateRestore(CSVData)

  If WinVer = "7" Then
    fe.Checked = False
    fe.Disabled = True
    Mux "FileExtension",False
  End If
  
  ArrCSV = Split(CSVData,",")

  j = 1
  For i = 0 to 7
    vm(i).Checked = ArrCSV(j)
    j = j + 1
  Next 
  For i = 0 to 6
    xm(i).Checked = ArrCSV(j)
    j = j + 1
  Next
  For Each Chk in ArrCB
    Document.GetElementByID(Chk).Checked = ArrCSV(j)
    j = j + 1
  Next
  NameWid = ArrCSV(j)
  PathWid = ArrCSV(j+1)
  ColShow = ArrCSV(j+2)
  ColMore = ArrCSV(j+3)
  Language = ArrCSV(j+4)
  If LangCount=0 Then Language = "English"
  
  ViewOps
  wn.Value = NameWid
  wp.Value = PathWid
  cs.Value = ColShow
 
End Sub

'Save interface selections to file
Sub HTAStateSave
  CSVData = Ver & ","
  For i = 0 to 7
    CSVData = CSVData & vm(i).Checked & ","
  Next 
  For i = 0 to 6
    CSVData = CSVData & xm(i).Checked & ","
  Next
  For Each Chk in ArrCB
    CSVData = CSVData & Document.GetElementByID(Chk).Checked & ","
  Next
  CSVData = CSVData & wn.Value & "," & wp.Value & ","
  CSVData = CSVData & ColShow & ","
  CSVData = CSVData & ColMore & ","
  CSVData = CSVData & Language
  Set oFile = oFSO.OpenTextFile(CSVFile,2,True,-1)
  oFile.Write CSVData
  oFile.Close

End Sub

'Run WinSetView.ps1 with parameters based on user selections
Sub Submit
  HTAStateSave
  ColShow = Replace(ColShow," ",",")
  ColMore = Replace(ColMore," ",",")

  If vm(0).Checked Then Mode = 0
  If vm(1).Checked Then Mode = 1
  If vm(2).Checked Then Mode = 4
  If vm(3).Checked Then Mode = 2
  If vm(4).Checked Then Mode = 5
  If vm(5).Checked Then Mode = 3
  If vm(6).Checked Then Mode = 6
  If vm(7).Checked Then Mode = 7

  If pc.Checked Then 
    If xm(0).Checked Then TPMode = 1
    If xm(1).Checked Then TPMode = 4
    If xm(2).Checked Then TPMode = 2
    If xm(3).Checked Then TPMode = 5
    If xm(4).Checked Then TPMode = 3
    If xm(5).Checked Then TPMode = 6
    If xm(6).Checked Then TPMode = 7
  Else
    TPMode = 0
  End If
  
  If ng.Checked Then NoGroup=1 Else NoGroup=0
  If xg.Checked Then TPNoGrp=1 Else TPNoGrp=0
  If gn.Checked Then Generic=1 Else Generic=0
  If ka.Checked Then SubMode=1 Else SubMode=0
  If se.Checked Then ShowExt=1 Else ShowExt=0
  If ch.Checked Then Columns=1 Else Columns=0

  NameWid=wn.Value
  PathWid=wp.Value
  
  ColShow = "'" & ColShow & "'"
  ColMore = "'" & ColMore & "'"
  Params = Mode & " " & ShowExt & " " & NoGroup & " " & Generic & " " & TPMode & " " & TPNoGrp
  Params = Params & " " & Columns & " " & ColShow & " " & ColMore & " " & NameWid & " " & PathWid & " " & SubMode
  Visible = 1
  RunScript
End Sub

Sub RunScript
  CmdLine = "Powershell.exe -ExecutionPolicy Bypass .\WinSetView.ps1 " & Params
  Set oFile = oFSO.CreateTextFile(AppData & "\WinSetViewParams.txt")
  oFile.Write Params 
  oFile.Close()
  oShell.Run CmdLine,Visible,False
  Window.Close
End Sub

'Restore Explorer foder views from backup reg file
'WinSetView.ps1 creates a unique backup file on each run
Sub Restore
  Params = "9"
  Visible = 1
  RunScript
End Sub

'Hide or show options based on current selections
Sub ViewOps
  If vm(0).Checked Then
    op.style.visibility = "hidden"
    h2.style.visibility = "hidden"
    co.style.visibility = "hidden"  
    th.style.visibility = "hidden"  
    mc.style.visibility = "hidden"
    Exit Sub
  End If  
  op.style.visibility = "visible"
  h2.style.visibility = "visible"
  th.style.visibility = "visible"  
  mc.style.visibility = "visible"  
  If ch.Checked Then
    co.style.visibility = "visible"
  Else
    co.style.visibility = "hidden"  
  End If
  If pc.Checked Then
    mc.style.visibility = "visible"
  Else
    mc.style.visibility = "hidden"  
  End If
  If ka.checked And gn.checked Then
    h2.style.visibility = "visible"
  Else
    h2.style.visibility = "hidden"  
  End If
End Sub

'ColShow contains column headings to be displayed in Details view
'ColMore contains additional right-click column heading options
Sub Mux(Label,Checked)
  ColShow = " " & Replace(ColShow,","," ") & " "
  ColMore = " " & Replace(ColMore,","," ") & " "
  If Checked Then
    ColShow = ColShow & " " & Label
    ColMore = Replace(ColMore," " & Label & " "," ")
  Else
    ColMore = ColMore & " " & Label
    ColShow = Replace(ColShow," " & Label & " "," ")
  End If
  ColShow = Trim(ColShow)
  ColMore = Trim(ColMore)
  ColShow = Replace(ColShow,"  "," ")
  ColMore = Replace(ColMore,"  "," ")
  cs.Value = ColShow
End Sub

Sub Help01
  MsgBox HT1,vbInformation,HH1
End Sub

Sub Help02
  HT2 = Replace(HT2,"`",Chr(34))
  MsgBox HT2,vbExclamation,HH2
End Sub
</script>
<head>
<style>
  body  {background-color:LemonChiffon; font-size:10pt; font-family:Arial}
  table {font-size:10pt; font-family:Arial; border-collapse:collapse}
  td    {padding: 0 15 0 0}
  #op   {margin-bottom:5pt}
  #th   {margin-bottom:5pt}
  #cs   {margin-bottom:5pt; margin-top:5pt; font-size:9pt; width:100%; height:40; white-space:nowrap; overflow:auto}
  #h1   {width:2em}
  #h2   {width:2em}
  #wn   {width:2em; margin-left:2pt}
  #wp   {width:2em; margin-left:2pt}
  #w1   {margin-left:10pt; display:inline}
  #w2   {margin-left:10pt; display:inline}
  #lang {width:15em; font-size:8pt}
  #vb   {white-space:nowrap; display:inline}
  #wd   {margin-left:10pt}
  #ka   {margin-top:3pt}
</style>
</head>
<body id=body>
<select id=Lang onchange='UpdateLang'>
</select>
<input id=wd type=radio name=vm onclick=ViewOps >#1#<br><br>
<div id=vb>
  <input type=radio name=vm Checked onclick=ViewOps >#2#
  <input type=radio name=vm onclick=ViewOps >#3#
  <input type=radio name=vm onclick=ViewOps >#4#
  <input type=radio name=vm onclick=ViewOps >#5#
  <input type=radio name=vm onclick=ViewOps >#6#
  <input type=radio name=vm onclick=ViewOps >#7#
  <input type=radio name=vm onclick=ViewOps >#8#
</div><br><br>
<input type=checkbox id=se Checked >#9#
<div id=op>
  <input type=checkbox id=ng Checked >#10#
  <input type=checkbox id=gn Checked onclick=ViewOps >#11#
  <input type=button id=h2 value=! onclick=Help02><br>
  <input type=checkbox id=ka onclick=ViewOps >#12#<br><br>
  <input type=checkbox id=ch Checked onclick=ViewOps >#13#<br>
</div>
<div id=co>
  <table>
  <tr><td>
  <input type=checkbox id=fp onclick='Mux "ItemFolderPathDisplay",fp.Checked' >#21#</td><td>
  <input type=checkbox id=dm Checked onclick='Mux "DateModified",dm.Checked' >#14#</td><td>
  <input type=checkbox id=sz Checked onclick='Mux "Size",sz.Checked' >#15#</td><td>
  <input type=checkbox id=ti Checked onclick='Mux "ItemType",ti.Checked' >#16#</td></tr>
  <tr><td>
  <input type=checkbox id=ff onclick='Mux "ItemFolderPathDisplayNarrow",ff.Checked' >#25#</td><td>
  <input type=checkbox id=dc onclick='Mux "DateCreated",dc.Checked' >#18#</td><td>
  <input type=checkbox id=fv onclick='Mux "FileVersion",fv.Checked' >#19#</td><td>
  <input type=checkbox id=tt onclick='Mux "ItemTypeText",tt.Checked' >#20#</td></tr>
  <tr><td>
  <input type=checkbox id=pi onclick='Mux "ItemPathDisplay",pi.Checked' >#29#</td><td>
  <input type=checkbox id=da onclick='Mux "DateAccessed",da.Checked' >#22#</td><td>
  <input type=checkbox id=fe onclick='Mux "FileExtension",fe.Checked' >#23#</td><td>
  <input type=checkbox id=tc onclick='Mux "ContentType",tc.Checked' >#24#</td></tr>
  <tr><td>
  <input type=checkbox id=fn onclick='Mux "ItemFolderNameDisplay",fn.Checked' >#17#</td><td>
  <input type=checkbox id=fa onclick='Mux "FileAttributes",fa.Checked' >#26#</td><td>
  <input type=checkbox id=tk onclick='Mux "Kind",tk.Checked' >#27#</td><td>
  <input type=checkbox id=tp onclick='Mux "PerceivedType",tp.Checked' >#28#</td></tr>
  <tr><td>
  <input type=checkbox id=fo onclick='Mux "FileOwner",fo.Checked' >#30#</td></tr>
  </table>
  <textarea id=cs readOnly=True></textarea><br>
  <input type=button id=h1 value=? onclick=Help01>
  <div id=w1>#31#<input type=text id=wn maxlength="2" value="34"></div>
  <div id=w2>#32#<input type=text id=wp maxlength="2" value="34"></div><br><br>
</div>
<div id=th>
  <input type=checkbox id=pc Checked onclick=ViewOps >#33#<br>
</div>
<div id=mc>
  <input type=radio name=xm >#2#
  <input type=radio name=xm Checked >#3#
  <input type=radio name=xm >#4#
  <input type=radio name=xm >#5#
  <input type=radio name=xm >#6#
  <input type=radio name=xm >#7#
  <input type=radio name=xm >#8#<br>
  <input type=checkbox id=xg Checked >#34#<br><br>
</div>
<input type=button value="#35# ~" onclick=Submit>
<input type=button id=yb value="#36# ~" onclick='HTAStateRestore UserData'>
<input type=button value="#37# ~" onclick='HTAStateRestore DefData'>
<input type=button id=rb value="#38# ~" onclick=Restore>
</body>
</html>
